buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.flywaydb:flyway-mysql:8.5.10'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'org.springframework.boot' version '2.7.1'
    id 'io.spring.dependency-management' version '1.0.12.RELEASE'
    id 'org.flywaydb.flyway' version '8.5.10'
}

group 'com.codeflix.admin.video.catalog.infrastructure'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

bootJar {
    archiveName 'application.jar'
    destinationDirectory.set(file("${rootProject.buildDir}/libs"))
}

dependencies {
    implementation(project(":domain"))
    implementation(project(":application"))

    implementation('mysql:mysql-connector-java:8.0.29')

    implementation('org.springdoc:springdoc-openapi-webmvc-core:1.6.9')
    implementation('org.springdoc:springdoc-openapi-ui:1.6.9')

    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude module: 'spring-boot-starter-tomcat'
    }
    implementation('org.springframework.boot:spring-boot-starter-undertow')
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')

    implementation('com.fasterxml.jackson.module:jackson-module-afterburner')

    testImplementation('org.flywaydb:flyway-core')
    testImplementation('org.flywaydb:flyway-mysql')
    testImplementation('org.springframework.boot:spring-boot-starter-test')

    testImplementation('org.testcontainers:testcontainers:1.17.2')
    testImplementation('org.testcontainers:mysql:1.17.2')
    testImplementation('org.testcontainers:junit-jupiter:1.17.2')

    testRuntimeOnly('com.h2database:h2')
}

flyway {
    url = System.getenv('FLYWAY_DB') ?: 'jdbc:mysql://localhost:3306/videos_adm'
    user = System.getenv('FLYWAY_USER') ?: 'root'
    password = System.getenv('FLYWAY_PASS') ?: '123456'
}

test {
    useJUnitPlatform()
}